plugins {
    id "com.jfrog.artifactory" version "4.9.8"
    id "maven-publish"
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 28
        versionCode 9
        versionName "2.0.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.0'
    testImplementation 'org.mockito:mockito-core:2.28.2'
}

publishing.publications {
    aar(MavenPublication) {
        groupId "TouchType"
        version = android.defaultConfig.versionName
        artifactId project.getName()

        // Tell maven to prepare the generated "*.aar" file for publishing
        artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
    }
}

artifactory {

    // artifactory properties with local defaults
    def user=project.properties['artifactory_user'] ?: "admin"
    def pass=project.properties['artifactory_pass'] ?: "password"
    def key=project.properties['artifactory_repo_key'] ?: "libs-release-local"
    def url=project.properties['artifactory_url'] ?: "http://localhost:8081/artifactory"

    contextUrl = url
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = key
            username = user
            password = pass
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true
        }
    }
}


